namespace TextAnalytics;

using Cadl.Http;

@route("/entities/health/jobs")
interface Health {
  @summary("Submit healthcare analysis job")
  @doc("Start a healthcare analysis job to recognize healthcare related entities (drugs, conditions, symptoms, etc) and their relations.")
  @post
  submit(
    ...CommonParameters,
    ...ShowStatsParameter,
    ...StringIndexTypeParameter,
    ...MultiLanguageInputParameter
  ): LongRunningResponse;

  @route("/{jobId}")
  @summary("Get healthcare analysis job status and results")
  @doc("Get details of the healthcare prediction job specified by the jobId.")
  getStatus(...JobParameters): Response<HealthcareJobState>;

  @route("/{jobId}")
  @summary("Cancel healthcare analysis job")
  @doc("Cancel healthcare prediction job.")
  @delete
  cancel(...JobIdParameter): LongRunningResponse;
}

model HealthcareJobState extends JobState {
  results: HealthcareResult;
}

model HealthcareResult is Result<DocumentHealthcareEntities> {}

model DocumentHealthcareEntities is DocumentInfo {
  @doc("Healthcare entities.")
  entities: HealthcareEntity[];

  @doc("Healthcare entity relations.")
  relations: HealthcareRelation[];
}

@doc("""
  Every relation is an entity graph of a certain relationType, where all entities
  are connected and have specific roles within the relation context.
  """)
model HealthcareRelation {
  @doc("The entities in the relation.")
  entities: HealthcareRelationEntity[];

  @doc("Type of relation.")
  relationType: HealthcareRelationType;
}

@knownValues(HealthcareRelationTypeKV)
model HealthcareRelationType is string {}
enum HealthcareRelationTypeKV {
  Abbreviation,
  DirectionOfBodyStructure,
  DirectionOfCondition,
  DirectionOfExamination,
  DirectionOfTreatment,
  DosageOfMedication,
  FormOfMedication,
  FrequencyOfMedication,
  FrequencyOfTreatment,
  QualifierOfCondition,
  RelationOfExamination,
  RouteOfMedication,
  TimeOfCondition,
  TimeOfEvent,
  TimeOfExamination,
  TimeOfMedication,
  TimeOfTreatment,
  UnitOfCondition,
  UnitOfExamination,
  ValueOfCondition,
  ValueOfExamination,
}

model HealthcareRelationEntity {
  @doc("Reference link object, using a JSON pointer RFC 6901 (URI Fragment Identifier Representation), pointing to the entity .")
  ref: string;

  @doc("""
    Role of entity in the relationship.    

    For example: 'CD20-positive diffuse large B-cell lymphoma' has the following entities with their 
    roles in parenthesis: CD20 (GeneOrProtein), Positive (Expression), diffuse large B-cell lymphoma 
    (Diagnosis).
    """)
  role: string;
}

// ISSUE: Can't get modelAsString=false for named enum: https://github.com/Azure/cadl-azure/issues/1036
@knownValues(ConditionalityKV)
model Conditionality is string {}
enum ConditionalityKV {
  hypothetical,
  conditional,
}

// ISSUE: Can't get modelAsString=false for named enum: https://github.com/Azure/cadl-azure/issues/1036
@knownValues(CertaintyKV)
model Certainty is string {}
enum CertaintyKV {
  positive,
  positivePossible,
  neutralPossible,
  negativePossible,
  negative,
}

@knownValues(AssociationKV)
model Association is string {}
enum AssociationKV {
  subject,
  other,
}

model HealthcareAssertion {
  @doc("Describes any conditionality on the entity.")
  conditionality?: Conditionality;

  @doc("Describes the entities certainty and polarity.")
  certainty?: Certainty;

  @doc("Describes if the entity is the subject of the text or if it describes someone else.")
  association?: Association;
}

model HealthcareEntityLink {
  @doc("Entity Catalog. Examples include: UMLS, CHV, MSH, etc.")
  dataSource: string;

  @doc("Entity id in the given source catalog.")
  id: string;
}

model HealthcareEntity {
  @doc("Entity text as appears in the request.")
  text: string;

  @doc("Healthcare Entity Category.")
  category: HealthcareEntityCategory;

  @doc("Entity sub type")
  subcategory: string;

  @doc("Start position for the entity text. Use of different 'stringIndexType' values can affect the offset returned.")
  offset: int32;

  @doc("Length for the entity text. Use of different 'stringIndexType' values can affect the length returned.")
  length: int32;

  @doc("Confidence score between 0 and 1 of the extracted entity.")
  confidenceScore: float64;
  assertion?: HealthcareAssertion;

  @doc("Preferred name for the entity. Example: 'histologically' would have a 'name' of 'histologic'.")
  name?: string;

  @doc("Entity references in known data sources.")
  links?: HealthcareEntityLink[];
}

@knownValues(HealthcareEntityCategoryKV)
model HealthcareEntityCategory is string {}
enum HealthcareEntityCategoryKV {
  BODY_STRUCTURE,
  AGE,
  GENDER,
  EXAMINATION_NAME,
  DATE,
  DIRECTION,
  FREQUENCY,
  MEASUREMENT_VALUE,
  MEASUREMENT_UNIT,
  RELATIONAL_OPERATOR,
  TIME,
  GENE_OR_PROTEIN,
  VARIANT,
  ADMINISTRATIVE_EVENT,
  CARE_ENVIRONMENT,
  HEALTHCARE_PROFESSION,
  DIAGNOSIS,
  SYMPTOM_OR_SIGN,
  CONDITION_QUALIFIER,
  MEDICATION_CLASS,
  MEDICATION_NAME,
  DOSAGE,
  MEDICATION_FORM,
  MEDICATION_ROUTE,
  FAMILY_RELATION,
  TREATMENT_NAME,
}
