namespace TextAnalytics;

using Cadl.Http;

@route("/entities/linking")
interface EntityLinking {
  @summary("Linked entities from a well known knowledge base")
  @doc("The API returns a list of recognized entities with links to a well known knowledge base. See the <a href=\"https://aka.ms/talangs\">Supported languages in Text Analytics API</a> for the list of enabled languages.")
  @post
  recognize(
    ...EntityLinkingParameters,
    ...ShowStatsParameter,
    ...MultiLanguageInputParameter
  ): Response<EntityLinkingResult>;
}

model EntityLinkingParameters {
  ...CommonParameters;
  ...StringIndexTypeParameter;
}

model EntityLinkingResult is Result<DocumentLinkedEntities> {}

model DocumentLinkedEntities is DocumentInfo {
  @doc("Recognized well known entities in the document.")
  entities: LinkedEntity[];
}

model LinkedEntity {
  @doc("Entity Linking formal name.")
  name: string;

  @doc("List of instances this entity appears in the text.")
  matches: Match[];

  @doc("Language used in the data source.")
  language: string;

  @doc("Unique identifier of the recognized entity from the data source.")
  id?: string;

  @doc("URL for the entity's page from the data source.")
  url: string;

  @doc("Data source used to extract entity linking, such as Wiki/Bing etc.")
  dataSource: string;

  @doc("Bing Entity Search API unique identifier of the recognized entity.")
  bingId?: string;
}

model Match {
  @doc("If a well known item is recognized, a decimal number denoting the confidence level between 0 and 1 will be returned.")
  confidenceScore: float64;

  @doc("Entity text as appears in the request.")
  text: string;

  @doc("Start position for the entity match text")
  offset: int32;

  @doc("Length for the entity match text.")
  length: int32;
}
