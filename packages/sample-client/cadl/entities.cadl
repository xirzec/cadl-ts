namespace TextAnalytics;

using Cadl.Http;

@route("/entities/recognition/general")
interface Entities {
  @summary("Named Entity Recognition")
  @doc("The API returns a list of general named entities in a given document. For the list of supported entity types, check <a href=\"https://aka.ms/taner\">Supported Entity Types in Text Analytics API</a>. See the <a href=\"https://aka.ms/talangs\">Supported languages in Text Analytics API</a> for the list of enabled languages.")
  @post
  recognize(
    ...EntitiesParameters,
    ...ShowStatsParameter,
    ...MultiLanguageInputParameter
  ): Response<EntitiesResult>;
}

model EntitiesParameters {
  ...CommonParameters;
  ...StringIndexTypeParameter;
}

model EntitiesResult is Result<DocumentEntities> {}

model Entity {
  @doc("Entity text as appears in the request.")
  text: string;

  @doc("Entity type.")
  category: string;

  @doc("Entity sub type.")
  subcategory?: string;

  @doc("""
    Start position for the entity text. 
    Use of different 'stringIndexType' values can affect the offset returned.
    """)
  offset: int32;

  @doc("""
    Length for the entity text. 
    Use of different 'stringIndexType' values can affect the length returned.
    """)
  length: int32;

  @doc("Confidence score between 0 and 1 of the extracted entity.")
  confidenceScore: float64;
}

model DocumentEntities is DocumentInfo {
  @doc("Recognized entities in the document.")
  entities: Entity[];
}
